apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.tapadoo.alerter.demo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.app_version_code
        versionName rootProject.ext.app_version_name

        archivesBaseName = "alerter-demo-$versionName"
    }

    signingConfigs {
        if (file(rootProject.ext.keystoreProperties).exists()) {
            def props = new Properties()
            file(rootProject.ext.keystoreProperties).withReader { reader ->
                props.load(reader)
            }

            release {
                storeFile file(props.STORE_FILE_RELEASE)
                storePassword props.STORE_PASSWORD
                keyAlias props.KEY_ALIAS
                keyPassword props.KEY_PASSORD
            }
        }
    }

    buildTypes {
        release {
            if (file(rootProject.ext.keystoreProperties).exists()) {
                signingConfig signingConfigs.release
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':alerter')
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}